library transformPins;
import SCPNConstructors;
import SCPNHelpers;

modeltype e4sm "strict" uses e4sm("http://de.tu-ilmenau/e4sm/1.0.0");
modeltype scpn "strict" uses scpn("http://timenet.tu-ilmenau.de/schema/SCPN");

configuration property logComponents : Boolean;

//mapping InputPin::transformInputPin(parentName:String, index: Integer) : PlaceType when {true} {
//init{
//	log("\t~Transforming FCFS input pin " + self.computeName());
// 	result:= new PlaceType(parentName +"_in_"+index.toString());
//}
//	log("\t~Transformed input pin " + result.label.text);
//}

-- maps an outputPin with raceSemantic = FCFS

property mergeArcNames : List(String) = List{"x","y","z","a","b","c","d","e","f","g"};

helper Pin::getDirection():String {
	    if(self.oclIsTypeOf(InputPin))
		    return "input";
		    return "output";
}

helper Pin::getDirectionShort():String {
	    if(self.oclIsTypeOf(InputPin))
		    return "_in_";
		    return "_out";
}

helper Pin::requiresSimpleTransformation() : Boolean{
   if(self.raceSemantic = RaceSemantic::FCFS or (self.getIncomingConnectors()->size()<2 and self.getOutgoingConnectors()->size()<2)){
   	return true;
   };
   	return false;
}

helper Pin::requiresMerge() : Boolean{
   if(self.raceSemantic = RaceSemantic::MERGE and self.getIncomingConnectors()->size() > 1){
   	return true;
   };
   if(self.raceSemantic = RaceSemantic::MERGE_AND_DUPLICATE and self.getIncomingConnectors()->size() > 1 and self.getOutgoingConnectors()->size() < 2){
   	return true;
   };
   	return false;
}

helper Pin::requiresDuplicate() : Boolean{
   if(self.raceSemantic = RaceSemantic::DUPLICATE and self.getOutgoingConnectors()->size() > 1){
   	return true;
   };
   if(self.raceSemantic = RaceSemantic::MERGE_AND_DUPLICATE and self.getOutgoingConnectors()->size() > 1 and self.getIncomingConnectors()->size() < 2){
   	return true;
   };
   	return false;
}

helper Pin::requiresMergeAndDuplicate() : Boolean{
   if(self.raceSemantic = RaceSemantic::MERGE_AND_DUPLICATE and self.getOutgoingConnectors()->size() > 1 and self.getIncomingConnectors()->size() > 1){
   	return true;
   };
   	return false;
}

// Despite its name, this is currently used for Inputs pins, currently
mapping Pin::simpleTransformOutputPin(parentName:String, index: Integer) : PlaceType 
where {self.raceSemantic = RaceSemantic::FCFS or (self.getIncomingConnectors()->size()<2 and self.getOutgoingConnectors()->size()<2)} {
	init{
	    var direction:String := self.getDirection();

		log("\t~Transforming FCFS " + self.getDirection() + " pin " + self.computeName());
	 	result:= new PlaceType(parentName + self.getDirectionShort() + index.toString());
	 	if(self.oclIsKindOf(e4sm::InputPin)){
	 		result.queue := self.oclAsType(e4sm::InputPin).queueType.convertQueueType();
	 	};
	}
	log("\t~Transformed FCFS " + self.getDirection() + " pin " + result.label.text);
}

mapping InputPin::simpleTransformInputPinWithCollect(parentName:String, index: Integer) :  pl :List(PlaceType), arcs : List(ArcType), it:List(ImmediateTransitionType)
where {self.collect > 1 and self.raceSemantic = RaceSemantic::FCFS or (self.getIncomingConnectors()->size()<2 and self.getOutgoingConnectors()->size()<2)} {
	init{
	    var direction:String := self.getDirection();

		log("\t~Transforming FCFS " + self.getDirection() + " pin " + self.computeName());
	 	var place : PlaceType := new PlaceType(parentName + self.getDirectionShort() + index.toString());
	 	if(self.oclIsKindOf(e4sm::InputPin)){
	 		place.queue := self.oclAsType(e4sm::InputPin).queueType.convertQueueType();
	 	};
	 	pl += place;
	 	
	 	//create all the needed arcs, places and iTr's so that the collection "loop" works
	 	var Place1 = new PlaceType(safeString(parentName + "_" + self.name) + "_collect");
		pl += Place1;
	
		var Place2 : PlaceType = new PlaceType(safeString(parentName + "_" + self.name) + "_collect_first");
		Place2.capacity := "1";
		pl += Place2;
					
		var Place3 = new PlaceType(safeString(parentName + "_" + self.name) + "_collect_rem");
		var place3Capacity = self.collect - 1;
		Place3.tokentype := "int";
		Place3.capacity:=place3Capacity.toString();
		pl += Place3;
		 
		var Transition2 = new ImmediateTransitionType(safeString(parentName + "_" + self.name) + "_immtrans_two");
		
		if(logComponents){
				Transition2.logfileName := safeString(parentName) + "_ready.csv";
				// TODO: generalize
				Transition2.logfileDescription := "current_time,size,pixelSize";
				Transition2.logfileExpression := "NOW,TAB,x.s,TAB,x.ps";
		};
		it += Transition2;
		 
		var Transition3 = new ImmediateTransitionType(safeString(parentName + "_" + self.name) + "_immtrans_three");
		if(logComponents){
				Transition3.logfileName := safeString(parentName) + "_ready.csv";
				// TODO: generalize
				Transition3.logfileDescription := "current_time,size,pixelSize";
				Transition3.logfileExpression := "NOW,TAB,x.s,TAB,x.ps";
		};
		
		it += Transition3;
		 
		var Transition4 = new ImmediateTransitionType(safeString(parentName + "_" + self.name) + "_immtrans_four");
		it += Transition4;
		 
		var Arc1 = new ArcType(safeString(parentName + "_" + self.name) + "_arc_one");
		Arc1.fromNode := Transition4.id;
		Arc1.toNode := place.id;
		Arc1.inscription.text := "x(s=x.s*" + self.collect.toString() + ")";
		arcs += Arc1;
		 
		var Arc2 = new ArcType(safeString(parentName + "_" + self.name) + "_arc_two");
		Arc2.fromNode := Place1.id;
		Arc2.toNode := Transition2.id;
		Arc2.inscription.text := "x";
		arcs += Arc2;
		 
		var Arc3 = new ArcType(safeString(parentName + "_" + self.name) + "_arc_three");
		Arc3.fromNode := Place1.id;
		Arc3.toNode := Transition3.id;
		Arc3.inscription.text := "x";
		arcs += Arc3;
		 
		var Arc4 = new ArcType(safeString(parentName + "_" + self.name) + "_arc_four");
		Arc4.fromNode := Transition2.id;
		Arc4.toNode := Place2.id;
		Arc4.inscription.text := "z";
		arcs += Arc4;
		 
		var Arc5 = new ArcType(safeString(parentName + "_" + self.name) + "_arc_five");
		Arc5.fromNode := Transition3.id;
		Arc5.toNode := Place2.id;
		Arc5.inscription.text := "x";
		arcs += Arc5;
		 
		var Arc6 = new ArcType(safeString(parentName + "_" + self.name) + "_arc_six");
		Arc6.fromNode := Place2.id;
		Arc6.toNode := Transition2.id;
		Arc6.inscription.text := "z";
		arcs += Arc6;
		 
		var Arc7 = new ArcType(safeString(parentName + "_" + self.name) + "_arc_seven");
		Arc7.fromNode := Transition2.id;
		Arc7.toNode := Place3.id;
		Arc7.inscription.text := "new(0)";
		arcs += Arc7;
		 
		var Arc8 = new ArcType(safeString(parentName + "_" + self.name) + "_arc_eight");
		Arc8.fromNode := Place3.id;
		Arc8.toNode := Transition4.id;
		Arc8.inscription.text := place3Capacity.toString() + "'y";
		arcs += Arc8;
		 
		var Arc9 = new ArcType(safeString(parentName + "_" + self.name) + "_arc_nine");
		Arc9.fromNode := Place2.id;
		Arc9.toNode := Transition4.id;
		Arc9.inscription.text := "x";
		arcs += Arc9;
	}
	log("\t~Transformed FCFS " + self.getDirection() + " pin " + place.label.text);
}

-- maps a pin with raceSemantic = MERGE
mapping Pin::transformPinMerge(parentName:String, index: Integer) : pl :List(PlaceType), arcs : List(ArcType), it:ImmediateTransitionType 
where {self.requiresMerge()} {
	init{
		//create the connecting immediate transition
		it := new ImmediateTransitionType(parentName + self.getDirectionShort() + index.toString() + "_merge");
	}
	log("\t~Transforming Merge " + self.getDirection() + " pin " + self.computeName());
    var mainPlace := new PlaceType(parentName + self.getDirectionShort() + index.toString());
 	pl += mainPlace;
 	
 	//create an arc between the immediate transition and the main pin place
 	var mainArc := new ArcType(parentName + self.getDirectionShort() + index.toString());
 	    mainArc.fromNode := it.id;
 	    mainArc.toNode := mainPlace.id;
 	    mainArc.inscription.text := "new({})"; // TODO: just a placeholder so far
 	arcs += mainArc;
 	    
 	var i:Integer := 1;
 	self.getIncomingConnectors()->forEach(ic){
 	    var place := new PlaceType(parentName + self.getDirectionShort() + i.toString()+"_"+index.toString());
 	    pl += place;
 	    var arc := new ArcType(parentName + self.getDirectionShort() + i.toString()+"_"+index.toString());
 	    arc.fromNode := place.id;
 	    arc.toNode := it.id;
 	    arc.inscription.text := mergeArcNames->at(i);
 	    arcs += arc;
 	    i := i + 1;
 	};
 	//log(result.toString());
	log("\t~Transformed Merge " + self.getDirection() + " pin " + mainPlace.label.text);
}

-- maps an outputPin with raceSemantic = DUPLICATE
mapping Pin::transformPinDuplicate(parentName:String, index: Integer) : pl :List(PlaceType), arcs : List(ArcType), it:ImmediateTransitionType 
where {self.requiresDuplicate()} {
	init{
		//create the connecting immediate transition
		it := new ImmediateTransitionType(parentName + self.getDirectionShort() + index.toString() + "_dupl");
	}
	log("\t~Transforming Duplicate " + self.getDirection() + " pin " + self.computeName());
    var mainPlace := new PlaceType(parentName + self.getDirectionShort() + index.toString());
 	pl += mainPlace;
 	

 	 	//create an arc between the main pin place and the immediate transition
 	 	var mainArc := new ArcType(parentName + self.getDirectionShort() + index.toString());
 	 	    mainArc.fromNode := mainPlace.id;
 	 	    mainArc.toNode := it.id;
 	 	arcs += mainArc;
 	 	    
 	 	var i:Integer := 1;
 	 	self.getOutgoingConnectors()->forEach(oc){
 	 	    var place := new PlaceType(parentName + self.getDirectionShort() + i.toString()+"_"+index.toString());
 	 	    pl += place;
 	 	    var arc := new ArcType(parentName + self.getDirectionShort() + i.toString()+"_"+index.toString());
 	 	    arc.fromNode := it.id;
 	 	    arc.toNode := place.id;
 	 	    arc.inscription.text := "copy " + arc.inscription.text;
 	 	    arcs += arc;
 	 	    i := i + 1;
 	 	};
 	//log(result.toString());
	log("\t~Transformed Duplicate " + self.getDirection() + " pin " + mainPlace.label.text);
}

-- maps a Pin with raceSemantic = MERGE_AND_DUPLICATE
mapping Pin::transformPinMergeAndDuplicate(parentName:String, index: Integer) : pl :List(PlaceType), arcs : List(ArcType), it:List(ImmediateTransitionType) 
where {self.requiresMergeAndDuplicate()} {
	init{
		//create the connecting immediate transition
	}
	log("\t~Transforming M&D " + self.getDirection() + " pin " + self.computeName());
	var itL := new ImmediateTransitionType(parentName + self.getDirectionShort() + index.toString() + "_mAndD_left");
	it += itL;
	var itR := new ImmediateTransitionType(parentName + self.getDirectionShort() + index.toString() + "_mAndD_right");
	it += itR;

    var mainPlace := new PlaceType(parentName + self.getDirectionShort() + index.toString());
 	pl += mainPlace;

 	 	//create an arc between the main pin place and the immediate transitions
 	 	var mainArcR := new ArcType(parentName + self.getDirectionShort() + index.toString() + "_1");
 	 	    mainArcR.fromNode := mainPlace.id;
 	 	    mainArcR.toNode := itR.id;
 	 	arcs += mainArcR;
 	 	
 	 	var mainArcL := new ArcType(parentName + self.getDirectionShort() + index.toString() +"_2");
 	 	    mainArcL.fromNode := itL.id;
 	 	    mainArcL.toNode := mainPlace.id;
 	 	    mainArcL.inscription.text := "new(1)"; //TODO: just a temporary placeholder so far
 	 	arcs += mainArcL;
 	 	    
 	 	var i:Integer := 1;
 	 	self.getOutgoingConnectors()->forEach(oc){
 	 	    var place := new PlaceType(parentName + self.getDirectionShort() + i.toString()+"outg_"+index.toString());
 	 	    pl += place;
 	 	    var arc := new ArcType(parentName + self.getDirectionShort() + i.toString()+"outg_"+index.toString());
 	 	    arc.fromNode := itR.id;
 	 	    arc.toNode := place.id;
 	 	    arc.inscription.text := "new(1)"; //TODO: just a placeholder so far
 	 	    arcs += arc;
 	 	    i := i + 1;
 	 	};
 	 	
 	 	//reset i to 1 and repeat for the incoming connectors
 	 	i := 1;
 	 	self.getIncomingConnectors()->forEach(ic){
	 	    var place := new PlaceType(parentName + self.getDirectionShort() + i.toString()+"inc_"+index.toString());
	 	    pl += place;
	 	    var arc := new ArcType(parentName + self.getDirectionShort() + i.toString()+"inc_"+index.toString());
	 	    arc.fromNode := place.id;
	 	    arc.toNode := itL.id;
	 	    arc.inscription.text := mergeArcNames->at(i);
	 	    arcs += arc;
	 	    i := i + 1;
 		};
 	//log(result.toString());
	log("\t~Transformed M&D " + self.getDirection() + " pin " + mainPlace.label.text);
}

/* These 2 methods would actually be better, but would not work without the connector
helper Pin::resolvePinWhenSource() {

}

helper Pin::resolvePinWhenTarget() {

}
*/

helper DataNode::resolvePinPlace (connector : Connector) : PlaceType{
	assert fatal (connector.source = self or connector.target = self) with log ("The given connector does not connect to the given data node. CONNECTOR: " + connector.toString() + " PIN: "+ self.toString());
    if(self.oclIsKindOf(Pin)) {
    var pin:Pin := self.oclAsType(Pin);
	if(connector.target = pin){
		log("... place for arc target pin: " + pin.computeName() + " " + pin.toString());
		var targetPlace : PlaceType := null;
		if(pin.requiresSimpleTransformation()) {
			if(not pin.oclAsType(InputPin).oclIsInvalid() and pin.oclAsType(InputPin).collect > 1){
				var res := pin.resolveIn(InputPin::simpleTransformInputPinWithCollect);
				targetPlace := res.pl->any(p : PlaceType | p.id = safeString(pin.container().oclAsType(Component).name + "_" + self.name) + "_collect" + placeSuffix); // This works for FCFS pins
				assert fatal (targetPlace <> null) with log ("It seems like the " + pin.getDirection() + " pin with collect wasn't transformed to any place " + pin.toString());
				return targetPlace;
			}
			else{
			targetPlace := pin.resolveone(PlaceType); // This works for FCFS pins
			assert fatal (targetPlace <> null) with log ("It seems like the " + pin.getDirection() + " pin wasn't transformed to any place " + pin.toString());
			return targetPlace;}
		}endif;
		
		log("Could not resolve as a simple place");
		if(pin.requiresMerge()){
			var searchTransformation := pin.resolveoneIn(Pin::transformPinMerge);
			log(searchTransformation.toString());
			var p : List(PlaceType) := searchTransformation.pl;
		
			assert fatal (p->size() > 1) with log ("It seems like the target merge " + pin.getDirection() + " pin wasn't transformed to any place " + pin.toString());
		    var i:=2; //List starts from 1, skip the first element (which is the main place)
		    pin.getIncomingConnectors()->forEach(ic){
			    if(ic = connector){
			    	return p->at(i);
			    }endif;
			    i := i+1;
		    }
		}else if(pin.requiresDuplicate()){
			var searchTransformation := pin.resolveoneIn(Pin::transformPinDuplicate);
			log(searchTransformation.toString());
			var p : List(PlaceType) := searchTransformation.pl;
		
			assert fatal (p->size() > 1) with log ("It seems like the target duplicate " + pin.getDirection() + " pin wasn't transformed to any place " + pin.toString());
		    return p->first(); //Return the main place
		}else { //pin.requiresMergeAndDuplicate() is true
		var searchTransformation := pin.resolveoneIn(Pin::transformPinMergeAndDuplicate);
			log(searchTransformation.toString());
			var p : List(PlaceType) := searchTransformation.pl;
		
			assert fatal (p->size() > 1) with log ("It seems like the target merge and duplicate " + pin.getDirection() + " pin wasn't transformed to any place " + pin.toString());
		    var i:=2 + pin.getOutgoingConnectors()->size(); //List starts from 1, skip the first element (which is the main place) and the places for the outgoingConnectors (which were created first)
		    pin.getIncomingConnectors()->forEach(ic){
			    if(ic = connector){
			    	return p->at(i);
			    }endif;
			    i := i+1;
		    }
		}
		endif;
		    
		assert fatal (targetPlace <> null) with log ("Can't find place replacing target " + pin.getDirection() + " pin " + pin.toString());
		return targetPlace;
	} else { // connector.source = pin, due to the assert above
		log("... place for arc source pin: " + pin.computeName() + " " + pin.toString());
		var sourcePlace : PlaceType := null;
		if(pin.requiresSimpleTransformation()) {
			sourcePlace := pin.resolveone(PlaceType); // This works for FCFS pins
			assert fatal (sourcePlace <> null) with log ("It seems like the FCFS source pin wasn't transformed to any place " + pin.toString());
			return sourcePlace;
		}endif;
		
		log("Could not resolve as a simple place");
		if(pin.requiresMerge()){
			var searchTransformation := pin.resolveoneIn(Pin::transformPinMerge);
			log(searchTransformation.toString());
			var p : List(PlaceType) := searchTransformation.pl;
		
			assert fatal (p->size() > 1) with log ("It seems like the source merge " + pin.getDirection() + " pin wasn't transformed to any place " + pin.toString());
		    return p->first(); // Return the main place
		}else if(pin.requiresDuplicate()){
			var searchTransformation := pin.resolveoneIn(Pin::transformPinDuplicate);
			log(searchTransformation.toString());
			var p : List(PlaceType) := searchTransformation.pl;
		
			assert fatal (p->size() > 1) with log ("It seems like the source duplicate " + pin.getDirection() + " pin wasn't transformed to any place " + pin.toString());
		    var i:=2; //List starts from 1, skip the first element (which is the main place)
		    pin.getOutgoingConnectors()->forEach(ic){
			    if(ic = connector){
			    	return p->at(i);
			    }endif;
			    i := i+1;
		    }
		    //targetPlace := p->any(pl | pl.capacity = "0");
		}else{ //pin.requiresMergeAndDuplicate() is true
		var searchTransformation := pin.resolveoneIn(Pin::transformPinMergeAndDuplicate);
			log(searchTransformation.toString());
			var p : List(PlaceType) := searchTransformation.pl;
		
			assert fatal (p->size() > 1) with log ("It seems like the source merge and duplicate " + pin.getDirection() + " pin wasn't transformed to any place " + pin.toString());
		    var i:=2; //List starts from 1, skip the first element (which is the main place)
		    pin.getOutgoingConnectors()->forEach(ic){
			    if(ic = connector){
			    	return p->at(i);
			    }endif;
			    i := i+1;
		    }
		}endif;
		    
		assert fatal (sourcePlace <> null) with log ("Can't find place replacing source " + pin.getDirection() + " pin " + pin.toString());
		return sourcePlace;
	}endif;
		return null;
	}else if(self.oclIsKindOf(DataStore)){
		var ds : DataStore := self.oclAsType(DataStore);
		var places : Tuple(arcs : List(ArcType), iTr : List(ImmediateTransitionType), pl : List(PlaceType)) := ds.resolveone(Tuple(arcs : List(ArcType), iTr : List(ImmediateTransitionType), pl : List(PlaceType)));
		if(connector.target = ds){
			log("Target is a datastore");
			var p : PlaceType := places.pl->selectOne(p | p.id.endsWith("_ds_in" + placeSuffix));
			assert fatal (not p.oclIsUndefined()) with log ("Could not find place for datastore used as target");
			return p;
		} else {
			// ds is connector.source
			log("Source is a datastore");
			var p : PlaceType := places.pl->selectOne(p | p.id.endsWith("_ds_out" + placeSuffix));
			assert fatal (not p.oclIsUndefined()) with log ("Could not find place for datastore used as source");
			return p;
		};
	}endif;
	return null;
}