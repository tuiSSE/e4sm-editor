module e4sm.de.metamodel.xtext.GenerateE4sm

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import com.github.darvasd.xtextdocs.xtext.fragment.DocsGeneratorFragment
import com.github.darvasd.xtextdocs.xtext.formatter.MarkdownDocsFormatter

var rootPath = ".."

Workflow {

	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "e4sm.de.metamodel.xtext"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\r\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
				preferXtendStubs = false
			}
		}
		language = StandardLanguage {
			name = "e4sm.de.metamodel.xtext.E4sm"
			fileExtensions = "e4smcode"
			referencedResource = "platform:/resource/e4sm.de.metamodel/model/e4sm.genmodel"
			referencedResource = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.genmodel"
			fragment = ecore2xtext.Ecore2XtextValueConverterServiceFragment2 auto-inject {}

			formatter = {
				generateStub = true
				generateXtendStub = true
			}
			scopeProvider = {
				generateStub = true
				generateXtendStub = true
			}

			serializer = {
				generateStub = false
			}
			validator = {
			// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			// Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
				generateDeprecationValidation = true
			}
			generator = {
				generateXtendStub = true
			}
			junitSupport = {
				junitVersion = "5"
			}

			// xtext-docs-gen https://github.com/darvasd/xtext-docs-gen
			fragment = DocsGeneratorFragment auto-inject {
				outputFileName = "xtext.md"
				formatter = MarkdownDocsFormatter {
					includeSimplifiedGrammar = true
					includeDotReferenceGraph = false
					mainTitle = "E4SM Textual Concrete Syntax Specification"// optional
				}
			}
		}
	}
}
